apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.github.Schinizer'

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

sourceSets.test {
    output.resourcesDir = output.classesDir
}

dependencies {
    compile "io.reactivex:rxjava:${rxjavaVersion}"

    compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    compile "com.squareup.okio:okio:${okioVersion}"

    compile "org.jsoup:jsoup:${jsoupVersion}"

    testCompile "junit:junit:${junitVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']

    pkg {
        repo = 'generic'
        name = 'RxUnfurl'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Schinizer/RxUnfurl.git'
    }

    version {
        name = 'v0.1.0'
        desc = 'RxUnfurl v0.1.0'
        released  = new Date()
        vcsTag = 'v0.1.0'
    }
}